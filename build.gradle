plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
}

group 'de.intersoft.hal.forms'
version '1.0.0'

sourceCompatibility = 1.8

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

repositories {
    jcenter()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    implementation 'de.otto.edison:edison-hal:2.0.1'
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    testImplementation platform('org.junit:junit-bom:5.3.1')

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.assertj:assertj-core:1.0.0'
    testImplementation 'org.skyscreamer:jsonassert:1.2.3'

    testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

artifacts {
    archives sourcesJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "Skaelch"
            name "Skaelch"
            email "ska@intersoft.de"
        }
    }

    scm {
        url "https://github.com/Skaelch/intersoft-hal-forms-util"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            groupId 'de.intersoft.hal.forms'
            artifactId 'de.intersoft.hal.forms'
            version '0.1.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An AWESOME lib. Really!')
                root.appendNode('name', 'de.intersoft.hal.forms')
                root.appendNode('url', 'https://github.com/Skaelch/intersoft-hal-forms-util')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['mavenPublication']

    pkg {
        repo = 'de.intersoft.hal.forms'
        name = 'intersoft-hal-forms-util'
        userOrg = 'skaelch'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Skaelch/intersoft-hal-forms-util'
        version {
            name = '0.1.0'
            desc = '0.1.0'
            released  = new Date()
        }
    }

}